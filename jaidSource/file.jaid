#foreign_library "kernel32"

FFILEACCESS :: enum s32
{
	GenericRead:		0x80000000,
	GenericWrite:		0x40000000,
	GenericExecute:		0x20000000,
	GenericAll:			0x10000000
}
	
FFILECREATE :: enum s32
{
	CreateNew: 			1,
	CreateAlways: 		2,
	OpenExisting: 		3,
	OpenAlways: 		4,
	TruncateExisting: 	5
}

FFILESHARE :: enum s32
{
	None:	0x0,
	Read:	0x1,
	Write:	0x2,
	Delete:	0x4
}

SLargeInteger :: struct  // tag = lgint
{
    m_nQuadPart: s64;
}

Bool32 :: typedef s32;

CreateFileA :: (
	pChzFilename: &u8,
	ffileaccess: FFILEACCESS,
	ffileshare: FFILESHARE,
	pVSecurity: & void,
	ffilecreate: FFILECREATE,
	ffileattrib: s32,
	hndTemplate: HANDLE) -> HANDLE #foreign #stdcall;

CloseHandle  :: (handle: HANDLE) -> s32 #foreign #stdcall;

GetFileSizeEx :: (hndFile: HANDLE, pLgint: & SLargeInteger) -> Bool32 #foreign #stdcall;

ReadFile32 :: (hndFile: HANDLE, pVBuffer: & void, cBRead: s32, pCBWasRead: & s32, pVOverlapped: &void) -> Bool32 #foreign ReadFile #stdcall;

SFile :: struct // tag = file
{
    m_hnd: HANDLE;
};


FileOpen :: (pChzFilename: & u8, fIsWriteMode: bool) -> SFile 
{
    file: SFile;
    if fIsWriteMode 
    {
        file.m_hnd = CreateFileA(
        	pChzFilename,
        	FFILEACCESS.GenericWrite,
        	FFILESHARE.Read,
        	null,	// pvSecurity
            FFILECREATE.CreateAlways,
            0, 		// ffileattrib
            0);		// hndTemplate
    }
    else 
    {
        file.m_hnd = CreateFileA(
        	pChzFilename,
        	FFILEACCESS.GenericRead,
        	FFILESHARE.Read,
        	null,	// pvSecurity
            FFILECREATE.OpenExisting,
            0, 		// ffileattrib
            0);		// hndTemplate
    }

    return file;
}

CloseFile :: (pFile: &SFile)
{
    CloseHandle(pFile.m_hnd);
}

PBReadEntireFile :: (pChzFilename: & u8, pCBWasRead: &s64) -> & u8
{
    @pCBWasRead = 0;
    file := FileOpen(pChzFilename, false);
    if file.m_hnd == INVALID_HANDLE
        return null;

    lgintSize: SLargeInteger;
    fSuccess := GetFileSizeEx(file.m_hnd, &lgintSize);
    if !fSuccess
    {
        CloseFile(&file);
        return null;
    }

    cBFile : s64 = lgintSize.m_nQuadPart;

    pBData : & u8 = cast (& u8) PVMalloc(cBFile);
    if !pBData
    {
        CloseFile(&file);
        return null;
    }
    
    @pCBWasRead = CBReadFile(file, pBData, cBFile);
    if (@pCBWasRead < cBFile)
    {
        FreeMalloc(pBData);
        CloseFile(&file);
        return null;
    }

    CloseFile(&file);
    @pCBWasRead = cBFile;
    return pBData;
}

CBReadFile :: (file: SFile, pB: & u8, cBLength: s64) -> s64
{
    cBSingleRead: s32; // need to split up the read to handle > 32bit read sizes
    cBWasRead: s64;
    fSuccess: bool;
    while cBWasRead < cBLength 
    {
        cBRemaining := cBLength - cBWasRead;
        cBToRead : s32;
        if cBRemaining <= 0x7fffffff 
        {
            cBToRead = cast(s32) cBRemaining;
        }
        else 
        {
            cBToRead = 0x7fffffff;
        }

        fSuccess = ReadFile32(file.m_hnd, pB + cBWasRead, cBToRead, &cBSingleRead, null);
        cBWasRead += cBSingleRead;
        if !fSuccess || cBSingleRead <= 0 
        { 
            return cBWasRead;
        }
    }

    return cBWasRead;
}
